plugins {
    id 'java-library'
    id 'jacoco'
    id 'com.github.kt3k.coveralls' version '2.10.1'
    id 'maven-publish'
    id 'com.jfrog.bintray' version '1.8.5'
    id 'com.jfrog.artifactory' version '4.15.2'
    id "com.github.ben-manes.versions" version "0.28.0"
    id "org.sonarqube" version "3.0"
}

sourceCompatibility = 1.8
targetCompatibility = 1.8
def versionName = '1.1.8-SNAPSHOT'

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.apache.logging.log4j:log4j-api:2.13.3'
    implementation 'io.netty:netty-all:4.1.50.Final'
    implementation 'javax.xml.bind:jaxb-api:2.3.1'

    testImplementation 'org.apache.logging.log4j:log4j-core:2.13.3'
    testImplementation 'org.junit.jupiter:junit-jupiter:5.6.2'
    testImplementation 'org.mockito:mockito-junit-jupiter:3.3.3'
    testImplementation('net.jradius:jradius-core:1.1.5') {
        transitive = false // for reference implementation of hashing utilities
    }
}

jacocoTestReport {
    reports {
        xml.enabled = true
        html.enabled = true
    }
}

sonarqube {
  properties {
    property "sonar.projectKey", "globalreachtech_tinyradius-netty"
    property "sonar.organization", "globalreachtech"
    property "sonar.host.url", "https://sonarcloud.io"
  }
}

coveralls {
    jacocoReportPath 'build/reports/jacoco/test/jacocoTestReport.xml'
}

test {
    useJUnitPlatform()
    testLogging {
        events "passed", "skipped", "failed", "standardOut", "standardError"
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    archiveClassifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    archiveClassifier = 'javadoc'
    from javadoc.destinationDir
}

bintray {
    user = ''
    key = ''
    publications = ['mavenJava']
    pkg {
        repo = 'grt-maven'
        name = 'tinyradius-netty'
        userOrg = 'globalreachtech'
        licenses = ['LGPL-2.1']
        vcsUrl = 'https://github.com/globalreachtech/tinyradius-netty.git'
        version {
            name = versionName
            released = new Date()
        }
    }
}

def pomConfig = {
    name 'tinyradius-netty'
    description 'TinyRadius is a simple, small and fast Java Radius library capable of sending and receiving Radius packets of all types.'
    url 'https://github.com/globalreachtech/tinyradius-netty'
    licenses {
        license {
            name 'GNU Lesser General Public License, version 2.1'
            url 'https://www.gnu.org/licenses/old-licenses/lgpl-2.1.html'
            distribution "repo"
        }
    }
    developers {
        developer {
            id 'globalreachtech'
            organizationUrl 'https://www.globalreachtech.com/'
        }
    }
    scm {
        url 'https://github.com/globalreachtech/tinyradius-netty'
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            afterEvaluate {
                artifact sourcesJar
                artifact javadocJar
            }
            groupId 'com.globalreachtech'
            artifactId 'tinyradius-netty'
            version versionName
            pom.withXml {
                asNode().children().last() + pomConfig
            }
        }
    }
}

